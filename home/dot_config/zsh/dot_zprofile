# shellcheck shell=bash

# Maintenance note: split this file across a zprofile.d directory
# if anything else needs to be added.

# ---------------------------------------------------------
# Prevent circular dependencies.

if [[ -n "$MH_PROCESSED_ZPROFILE" ]]; then
  echo "WARN: Already processed .zprofile, suggesting a circular dependency" >&2
  return
fi

# ---------------------------------------------------------
# Activate Homebrew (macOS and x86 Linux), or Nix Home Manager (Linux arm64).
#
# Do this here in .zprofile rather than in .zshenv intentionally:
# For login shells, startup order includes .zshenv, /etc/zprofile, .zprofile.
# MacOS makes changes to PATH within /etc/zprofile.
# So activating homebrew/home-manager here allows more control of its precedence in PATH.

if [[ "$(uname -s)" == "Darwin" ]]; then
  if [[ -f "/opt/homebrew/bin/brew" ]]; then
    eval "$(/opt/homebrew/bin/brew shellenv)"
  else
    echo "WARN: Homebrew not set up. Suggestion: $ mh-install" >&2
  fi

elif [[ "$(uname -s)" == "Linux" && "$(uname -m)" != "aarch64" ]]; then
  if [[ -f "/home/linuxbrew/.linuxbrew/bin/brew" ]]; then
    eval "$(/home/linuxbrew/.linuxbrew/bin/brew shellenv)"
    echo "Homebrew activated"
  else
    echo "WARN: Linux Homebrew not set up. Suggestion: $ mh-install" >&2
  fi

elif [[ "$(uname -s)" == "Linux" && "$(uname -m)" == "aarch64" ]]; then
  # Note: Keep this OS-specific activation, as both Nix *and* these files
  # are present in my devcontainer config for x86, but are intended to be left dormant.
  if [[ -f "$HOME/.nix-profile/etc/profile.d/hm-session-vars.sh" ]]; then
    source "$HOME/.nix-profile/etc/profile.d/hm-session-vars.sh"
  else
    echo "WARN: Nix Home Manager not set up. Suggestion: $ mh-install" >&2
  fi

fi

# ---------------------------------------------------------
# Prezto

source_prezto_runcoms zprofile

# ---------------------------------------------------------
# Mark as processed.

MH_PROCESSED_ZPROFILE=true

# ---------------------------------------------------------
